apply from: "$project.rootDir/libraries/ui-components/tools/dependencies/ui-components-libraries.gradle"

dependencies { configuration ->
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    qualityAssuranceDependencies(libraries, configuration) /* Quality Assurance */
    kotlinDependencies(libraries, configuration)           /* Kotlin */
    materialDesignDependencies(libraries, configuration)   /* Android Material Design */

    // add more dependency groups here
}

/** QA Dependencies Group. */
private static void qualityAssuranceDependencies(libraries, configuration) {
    configuration.androidTestImplementation(libraries.qualityAssurance.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    configuration.androidTestImplementation libraries.qualityAssurance.espressoRunner
    configuration.androidTestImplementation libraries.qualityAssurance.jUnitAndroidX
    configuration.testImplementation        libraries.qualityAssurance.jUnit

    // add more QA dependencies here if necessary
}

/** Kotlin Dependencies Group. */
private static void kotlinDependencies(libraries, configuration) {
    configuration.implementation libraries.kotlin.base

    // add more Kotlin dependencies here if necessary
}

/** Android Material Design Dependencies Group. */
private static void materialDesignDependencies(libraries, configuration) {
    configuration.api libraries.materialDesign.base
    configuration.api libraries.materialDesign.recyclerView
    configuration.api libraries.materialDesign.cardView

    // add more Android Material Design dependencies here if necessary
}
