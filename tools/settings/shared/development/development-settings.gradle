final String DEV_BUILD = 'devBuild'

/* Uncomment the lines below to force all dependency download - This costs build performance! */
//configurations.all {
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // check for updates every build
//}

android {

    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false // Crashlytics will generate a new build id for every build otherwise
        }
    }

    /**
     * Setup Gradle for faster dev build times
     * See <a href="https://www.youtube.com/watch?v=7ll-rkLCtyk">
     *     Speeding Up Your Android Gradle Builds (Google I/O '17)</a>
     */
    if (project.hasProperty(DEV_BUILD)) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    /**
     * Setup Data Binding for resource binding.
     *
     * Better alternative than using ButterKnife external dependency.
     * See <a href="https://hackernoon.com/android-butterknife-vs-data-binding-fffceb77ed88">
     *      Android Butterknife vs Data Binding</a>
     *
     * No more findById() calls.
     * See <a href="https://academy.realm.io/posts/360-andev-2017-ben-mccanny-data-binding">
     *      Data Binding in Practice by Ben McCanny</a>
     */
    buildFeatures {
        dataBinding = true
    }

    lintOptions {
        disable 'InvalidPackage' // Necessary because of OkHttp, which is not an Android package
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

}

kapt {
    correctErrorTypes true
    //TODO
}
