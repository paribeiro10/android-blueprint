apply from: "$project.rootDir/features/static/onboarding/tools/dependencies/onboarding-libraries.gradle"

dependencies { configuration ->
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    qualityAssuranceDependencies(libraries, configuration) /* Quality Assurance */
    kotlinDependencies(libraries, configuration)           /* Kotlin */
    injectionDependencies(libraries, configuration)        /* Dependency Injection Frameworks */

    /* In Project Static Multi-Modules */
    implementation project(':libraries:architecture-components')
    implementation project(':libraries:ui-components')

    // add more dependency groups here
}

/** QA Dependencies Group. */
private static void qualityAssuranceDependencies(libraries, configuration) {
    configuration.androidTestImplementation(libraries.qualityAssurance.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    configuration.androidTestImplementation libraries.qualityAssurance.espressoRunner
    configuration.androidTestImplementation libraries.qualityAssurance.jUnitAndroidX
    configuration.testImplementation        libraries.qualityAssurance.jUnit

    // add more QA dependencies here if necessary
}

/** Kotlin Dependencies Group. */
private static void kotlinDependencies(libraries, configuration) {
    configuration.implementation libraries.kotlin.base

    // add more Kotlin dependencies here if necessary
}

/** Dependency Injection Dependencies Group. */
private static void injectionDependencies(libraries, configuration) {
    configuration.implementation libraries.daggerHilt.base
    configuration.kapt libraries.daggerHilt.compiler

    // add more dependencies here
}
